// BSLLS-off
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список тестов
//
Функция ПолучитьСписокТестов() Экспорт

	Тесты = Новый Массив;
	Тесты.Добавить("Тест_РазобратьURLСоЗнакомРавноВЗначенииПараметраЗапроса");
	Тесты.Добавить("Тест_РазобратьURLСПараметромЗапросаБезЗначения");
	Тесты.Добавить("Тест_РазобратьURLСЯвноЗаданнымПортом");
	Тесты.Добавить("Тест_РазобратьURLСПараметрамиЗапросаСНесколькимиЗначениями");
	Тесты.Добавить("Тест_РазобратьURLСФрагментом");
	Тесты.Добавить("Тест_РазобратьURLСХостомВФорматеIPv6");
	Тесты.Добавить("Тест_РазобратьПростойURL");
	Тесты.Добавить("Тест_РазобратьURLСЗакодированнымURLВПараметре");
	Тесты.Добавить("Тест_РазобратьURLСИменемПользователяИПаролем");
	Тесты.Добавить("Тест_РазобратьURLСИменемПользователяБезПароля");
	
	Тесты.Добавить("Тест_HMAC");
	
	Если ТестироватьСоединениеЧерезПрокси Тогда
		Тесты.Добавить("Тест_СоединениеЧерезПрокси");
	КонецЕсли;
	Если ТестироватьПолучениеСпискаРелизовВСайта1С Тогда
		Тесты.Добавить("Тест_ПолучениеСпискаРелизовВСайта1С");
	КонецЕсли;
	Тесты.Добавить("Тест_ПараметрыЗаписиJson");
	Тесты.Добавить("Тест_URLБезСхемы");
	Тесты.Добавить("Тест_ПередачаПараметровВСтрокуЗапроса");
	Тесты.Добавить("Тест_ПередачаПараметровВСтрокуЗапросаКомбинированный");
	Тесты.Добавить("Тест_РезультатКакJsonGet");
	Тесты.Добавить("Тест_РезультатКакJsonPost");
	Тесты.Добавить("Тест_РезультатКакДвоичныеДанные");
	Тесты.Добавить("Тест_РезультатКакТекст");
	Тесты.Добавить("Тест_ПередачаПроизвольныхЗаголовков");
	Тесты.Добавить("Тест_ОтправкаДанныхФормы");
	Тесты.Добавить("Тест_ОтправкаJson");
	Тесты.Добавить("Тест_Таймаут");
	Тесты.Добавить("Тест_BasicAuth");
	Тесты.Добавить("Тест_DigestAuth");
#Если Не МобильноеПриложениеСервер Тогда
	Тесты.Добавить("Тест_ОтправитьGZip");
	Тесты.Добавить("Тест_ПолучитьGZip");
	Тесты.Добавить("Тест_УпаковатьGZip");
	Тесты.Добавить("Тест_РаспаковатьGZip");
#КонецЕсли
	Тесты.Добавить("Тест_GetJson");
	Тесты.Добавить("Тест_PostJson");
	Тесты.Добавить("Тест_PostИРедирект");
	Тесты.Добавить("Тест_GetJsonСтруктура");
	Тесты.Добавить("Тест_PutJson");
	Тесты.Добавить("Тест_DeleteJson");
	Тесты.Добавить("Тест_GetУспешныйРедиректОтносительныйАдрес");
	Тесты.Добавить("Тест_GetУспешныйРедиректАбсолютныйАдрес");
	Тесты.Добавить("Тест_GetЗацикленныйРедирект");
	Тесты.Добавить("Тест_GetОтключенныйРедирект");
	Тесты.Добавить("Тест_РедиректСУказаниемURL");
	Тесты.Добавить("Тест_Ошибка404");
	Тесты.Добавить("Тест_РаботаССессиями");
	Тесты.Добавить("Тест_Options");
	Тесты.Добавить("Тест_Head");
	Тесты.Добавить("Тест_Delete");
	Тесты.Добавить("Тест_Patch");
	Тесты.Добавить("Тест_ПроизвольныйМетод");
	Тесты.Добавить("Тест_УстановкаCookies");
	Тесты.Добавить("Тест_ОтправитьCookies");
	Тесты.Добавить("Тест_POST_MultipartFormData_ТолькоФайл");
	Тесты.Добавить("Тест_POST_MultipartFormData_ФайлыИПоляФормы");
	Тесты.Добавить("Тест_POST_MultipartFormData_ФайлыИПоляФормы_КонструкторПараметров");
	Тесты.Добавить("Тест_ПараметрыЗапросаТолькоКлюч");
	Тесты.Добавить("Тест_ОтправкаXml");
	Тесты.Добавить("Тест_ПрочитатьОтветКакXDTO");
	Тесты.Добавить("Тест_СложныеПараметрыЗапроса");
	Тесты.Добавить("Тест_PostПустойJson");
	Если ТестироватьАутентификациюAWS4_HMAC_SHA256 Тогда
		Тесты.Добавить("Тест_АутентификацияAWS4_HMAC_SHA256");
	КонецЕсли;
	
	Тесты.Добавить("Тест_АутентификацияAWS4_HMAC_SHA256_ПустоеТелоПортНеУказан");
	Тесты.Добавить("Тест_АутентификацияAWS4_HMAC_SHA256_ПустоеТелоУказанСтандартныйПорт");
	Тесты.Добавить("Тест_АутентификацияAWS4_HMAC_SHA256_ПустоеТелоУказанНеСтандартныйПорт");
	
	Если ТестироватьПовторы Тогда
		Тесты.Добавить("Тест_ПовторСУчетомЗаголовкаRetryAfterDate");
		Тесты.Добавить("Тест_ПовторСУчетомЗаголовкаRetryAfterDuration");
		Тесты.Добавить("Тест_ПовторПослеОшибки502");
		Тесты.Добавить("Тест_ПовторПослеОшибкиПодключения");
		Тесты.Добавить("Тест_Ping");
	КонецЕсли;

	Тесты.Добавить("Тест_ПредставлениеКодаСостоянияHTTP");
	Тесты.Добавить("Тест_ПредставлениеНеизвестногоКодаСостоянияHTTP");
	Тесты.Добавить("Тест_КорректноеИсключениеВМетодеКакJson");
	Тесты.Добавить("Тест_ПроверкаПреобразованияВJsonНеСериализуемыхЗначений");
	Тесты.Добавить("Тест_ПроверкаПреобразованияВариантаЗаписиДатыJSON");
	Тесты.Добавить("Тест_ПроверкаВосстановленияНеподдерживаемыхТиповЗначений");

	Тесты.Добавить("Тест_ОтправкаЗапросаПортитПереданныеНастройки_GitHub_Issue_33");

	Возврат Тесты;
	
КонецФункции

// Запускает прогон тестов
//
Функция ВыполнитьТестыНаСервере(Тесты) Экспорт

	Для Каждого СтрокаТаблицы Из Тесты Цикл
		Если Не СтрокаТаблицы.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			Выполнить(СтрокаТаблицы.Тест + "()");
			СтрокаТаблицы.Результат = 
				НСтр("ru = 'OK'");
			СтрокаТаблицы.Ошибка = "";
		Исключение
			СтрокаТаблицы.Результат =
				НСтр("ru = 'FAIL'");
			СтрокаТаблицы.Ошибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
	КонецЦикла;
	
	Возврат Тесты;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Тесты

Процедура Тест_РазобратьURLСоЗнакомРавноВЗначенииПараметраЗапроса() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL(
		"https://httpbin.org/anything?jql=worklogDate >= 2017-04-01 AND worklogDate <= 2017-05-01&j&i=2"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "https"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "httpbin.org"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/anything"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса["jql"], "worklogDate >= 2017-04-01 AND worklogDate <= 2017-05-01"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса["j"], Неопределено); //@non-nls-1
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса["i"], "2"); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_РазобратьURLСПараметромЗапросаБезЗначения() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL("https://httpbin.org/get?key"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "https"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "httpbin.org"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/get"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса["key"], Неопределено); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_РазобратьURLСЯвноЗаданнымПортом() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL("https://httpbin.org:443/get?key"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "https"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "httpbin.org"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/get"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Порт, 443);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса["key"], Неопределено); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_РазобратьURLСПараметрамиЗапросаСНесколькимиЗначениями() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL("http://httpbin.org/anything?i=v1&j=w1&j=w2&i=v2&i=v3"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "http"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "httpbin.org"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/anything"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтрСоединить(СтруктураURL.ПараметрыЗапроса["i"], ", "), "v1, v2, v3"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(СтрСоединить(СтруктураURL.ПараметрыЗапроса["j"], ", "), "w1, w2"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_РазобратьURLСФрагментом() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL("https://en.wikipedia.org/wiki/1C_Company#Products"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "https"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "en.wikipedia.org"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/wiki/1C_Company"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "Products"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса.Количество(), 0);
	
КонецПроцедуры

Процедура Тест_РазобратьURLСХостомВФорматеIPv6() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL("http://[2001:0db8:11a3:09d7:1f34:8a2e:07a0:765d]:8080/test"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "http"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "[2001:0db8:11a3:09d7:1f34:8a2e:07a0:765d]"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/test"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Порт, 8080);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса.Количество(), 0);
	
КонецПроцедуры

Процедура Тест_РазобратьПростойURL() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL("http://1c.ru"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "http"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "1c.ru"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/");
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса.Количество(), 0);
	
КонецПроцедуры

Процедура Тест_РазобратьURLСЗакодированнымURLВПараметре() Экспорт
	
	СтруктураURL = КоннекторHTTP.РазобратьURL(
		"https://www.example.ru?url=http%3A%2F%2Fwww.kuku.ru%2F%3Fs%3D1%26b%3D2&OTHER=1"); //@non-nls-1
	
	УтверждениеВерно(СтруктураURL.Схема, "https"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "www.example.ru"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/");
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса.Количество(), 2);
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса["url"], "http://www.kuku.ru/?s=1&b=2"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса["OTHER"], "1"); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_РазобратьURLСИменемПользователяИПаролем() Экспорт

	СтруктураURL = КоннекторHTTP.РазобратьURL("http://user:pass@1c.ru"); //@non-nls-1

	УтверждениеВерно(СтруктураURL.Схема, "http"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "1c.ru"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/");
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "user"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "pass"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса.Количество(), 0);

КонецПроцедуры

Процедура Тест_РазобратьURLСИменемПользователяБезПароля() Экспорт

	СтруктураURL = КоннекторHTTP.РазобратьURL("http://user@1c.ru"); //@non-nls-1

	УтверждениеВерно(СтруктураURL.Схема, "http"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Сервер, "1c.ru"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Путь, "/");
	УтверждениеВерно(СтруктураURL.Порт, 0);
	УтверждениеВерно(СтруктураURL.Фрагмент, "");
	УтверждениеВерно(СтруктураURL.Аутентификация.Пользователь, "user"); //@non-nls-1
	УтверждениеВерно(СтруктураURL.Аутентификация.Пароль, "");
	УтверждениеВерно(СтруктураURL.ПараметрыЗапроса.Количество(), 0);

КонецПроцедуры

Процедура Тест_HMAC() Экспорт
	
	Ключ = ПолучитьДвоичныеДанныеИзСтроки("Секретный ключ", КодировкаТекста.UTF8, Ложь); //@non-nls-1
	КлючБольше64 = ПолучитьДвоичныеДанныеИзСтроки(
		"1234567890123456789012345678901234567890123456789012345678901234567890", КодировкаТекста.UTF8, Ложь); //@non-nls-1
	
	ПустыеДанные = ПолучитьДвоичныеДанныеИзСтроки("", КодировкаТекста.UTF8, Ложь);
	Данные = ПолучитьДвоичныеДанныеИзСтроки("Какие-то данные", КодировкаТекста.UTF8, Ложь); //@non-nls-1
	
	УтверждениеВерно(
		КоннекторHTTP.HMAC(Ключ, ПустыеДанные, ХешФункция.MD5), 
		ПолучитьДвоичныеДанныеИзHexСтроки("1b1ec4166a11c03b3afefaea442e7709")); //@non-nls-1
	УтверждениеВерно(
		КоннекторHTTP.HMAC(КлючБольше64, Данные, ХешФункция.MD5), 
		ПолучитьДвоичныеДанныеИзHexСтроки("ed5b2d6b9f573cd46e8f8d1d1e8b70e3")); //@non-nls-1
		
	УтверждениеВерно(
		КоннекторHTTP.HMAC(Ключ, ПустыеДанные, ХешФункция.SHA1), 
		ПолучитьДвоичныеДанныеИзHexСтроки("355aa0587050d711f4ca9af6930c736363a88d34")); //@non-nls-1
	УтверждениеВерно(
		КоннекторHTTP.HMAC(КлючБольше64, Данные, ХешФункция.SHA1), 
		ПолучитьДвоичныеДанныеИзHexСтроки("7e8f9d7ebbe81e508a39f410e157fc6e714b3371")); //@non-nls-1
		
	УтверждениеВерно(
		КоннекторHTTP.HMAC(Ключ, ПустыеДанные, ХешФункция.SHA256),
		ПолучитьДвоичныеДанныеИзHexСтроки("70907d03476d72b7276897718590a49f6ce56991112fb5a0e9ed41652b2aab6c")); //@non-nls-1
	УтверждениеВерно(
		КоннекторHTTP.HMAC(КлючБольше64, Данные, ХешФункция.SHA256),
		ПолучитьДвоичныеДанныеИзHexСтроки("80be8107de7879f028c8bfe97e10b859785530dd19dfc41a4d6962ce4c2fc130")); //@non-nls-1

КонецПроцедуры

Процедура Тест_СоединениеЧерезПрокси() Экспорт
	
	Прокси = Новый ИнтернетПрокси;
	Прокси.Установить("http", ПроксиСервер, ПроксиПорт); //@non-nls-1
	КоннекторHTTP.GetJson(
		"http://httpbin.org/headers", //@non-nls-1
		Неопределено,
		Новый Структура("Прокси", Прокси)
	);

КонецПроцедуры

Процедура Тест_ПараметрыЗаписиJson() Экспорт
	
	ПараметрыЗаписиJSON = Новый Структура("ПереносСтрок", ПереносСтрокJSON.Нет);
	Json = Новый Структура;
	Json.Вставить("field", "value"); //@non-nls-1 //@non-nls-2
	Json.Вставить("field2", "value2"); //@non-nls-1 //@non-nls-2
	Результат = КоннекторHTTP.PostJson(
		"http://httpbin.org/anything", //@non-nls-1
		Json,
		Новый Структура("ПараметрыЗаписиJSON", ПараметрыЗаписиJSON)
	);
	УтверждениеВерно(Результат["data"], "{""field"":""value"",""field2"":""value2""}"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_URLБезСхемы() Экспорт
	
	Ответ = КоннекторHTTP.Get("httpbin.org/get"); //@non-nls-1
	КоннекторHTTP.КакJson(Ответ);
 	УтверждениеВерно(Ответ.URL, "http://httpbin.org/get"); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_ПередачаПараметровВСтрокуЗапроса() Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("name", СтрРазделить("Иванов,Петров", ",")); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("salary", Формат(100000, "ЧГ=")); //@non-nls-1
	ПараметрыЗапроса.Вставить("time", "01:47"); //@non-nls-1
	
	Ответ = КоннекторHTTP.Get("https://httpbin.org/anything/params", ПараметрыЗапроса);//@non-nls-1
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Ответ.URL, "https://httpbin.org/anything/params?name=%D0%98%D0%B2%D0%B0%D0%BD%D0%BE%D0%B2&name=%D0%9F%D0%B5%D1%82%D1%80%D0%BE%D0%B2&salary=100000&time=01%3A47"); //@non-nls-1
	УтверждениеВерно(Результат["url"], "https://httpbin.org/anything/params?name=Иванов&name=Петров&salary=100000&time=01%3A47"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["args"]["salary"], "100000"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(СтрСоединить(Результат["args"]["name"], ","), "Иванов,Петров"); //@non-nls-1 //@non-nls-2 //@non-nls-4
	УтверждениеВерно(Результат["args"]["time"], "01:47"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_ПередачаПараметровВСтрокуЗапросаКомбинированный() Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("name", СтрРазделить("Иванов,Петров", ",")); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("salary", Формат(100000, "ЧГ=")); //@non-nls-1
	
	Результат = КоннекторHTTP.GetJson("https://httpbin.org/anything/params?post=Программист", ПараметрыЗапроса); //@non-nls-1
	
	УтверждениеВерно(Результат["args"]["salary"], "100000"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["args"]["post"], "Программист"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(СтрСоединить(Результат["args"]["name"], ","), "Иванов,Петров"); //@non-nls-1 //@non-nls-2 //@non-nls-4
	
КонецПроцедуры

Процедура Тест_РезультатКакJsonGet() Экспорт
	
	Результат = КоннекторHTTP.GetJson("https://httpbin.org/get"); //@non-nls-1
	УтверждениеВерно(Результат["url"], "https://httpbin.org/get"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_РезультатКакJsonPost() Экспорт
	
	Результат = КоннекторHTTP.КакJson(КоннекторHTTP.Post("https://httpbin.org/post")); //@non-nls-1
	УтверждениеВерно(Результат["url"], "https://httpbin.org/post"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_РезультатКакДвоичныеДанные() Экспорт
	
	Результат = КоннекторHTTP.КакДвоичныеДанные(КоннекторHTTP.Get("http://httpbin.org/image/png")); //@non-nls-1
	
	УтверждениеВерно(ТипЗнч(Результат), Тип("ДвоичныеДанные"));
	УтверждениеВерно(ПосчитатьMD5(Результат), "5cca6069f68fbf739fce37e0963f21e7"); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_РезультатКакТекст() Экспорт
	
	Результат = КоннекторHTTP.КакТекст(КоннекторHTTP.Get("http://httpbin.org/encoding/utf8")); //@non-nls-1
	УтверждениеВерно(СтрНайти(Результат, "Зарегистрируйтесь сейчас на Десятую Международную"), 3931); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_ПередачаПроизвольныхЗаголовков() Экспорт
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-My-Header", "Hello"); //@non-nls-1 //@non-nls-2
	Результат = КоннекторHTTP.GetJson(
		"http://httpbin.org/headers", //@non-nls-1
		Неопределено,
		Новый Структура("Заголовки", Заголовки)
	);
	
	УтверждениеВерно(Результат["headers"]["X-My-Header"], "Hello"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_ОтправкаДанныхФормы() Экспорт
	
	Данные = Новый Структура;
	Данные.Вставить("comments", "Постучать в дверь"); //@non-nls-1 //@non-nls-2
	Данные.Вставить("custemail", "vasya@mail.ru"); //@non-nls-1 //@non-nls-2
	Данные.Вставить("custname", "Вася"); //@non-nls-1 //@non-nls-2
	Данные.Вставить("custtel", "112"); //@non-nls-1
	Данные.Вставить("delivery", "20:20"); //@non-nls-1
	Данные.Вставить("size", "medium"); //@non-nls-1 //@non-nls-2
	Данные.Вставить("topping", СтрРазделить("bacon,mushroom", ",")); //@non-nls-1 //@non-nls-2
	
	Ответ = КоннекторHTTP.Post("http://httpbin.org/post", Данные); //@non-nls-1
	УтверждениеВерно(Ответ.URL, "http://httpbin.org/post"); //@non-nls-1
	Результат = КоннекторHTTP.КакJson(Ответ);
	УтверждениеВерно(Результат["form"]["size"], "medium"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["form"]["comments"], "Постучать в дверь"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["form"]["custemail"], "vasya@mail.ru"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["form"]["delivery"], "20:20"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["form"]["custtel"], "112");	//@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_ОтправкаJson() Экспорт
	
	Json = Новый Структура;
	Json.Вставить("Сотрудник", "Иванов Иван Петрович"); //@non-nls-1 //@non-nls-2
	Json.Вставить("Должность", "Разнорабочий"); //@non-nls-1 //@non-nls-2
	
	Результат = КоннекторHTTP.PostJson("http://httpbin.org/post", Json); //@non-nls-1
	
	УтверждениеВерно(Результат["json"]["Сотрудник"], "Иванов Иван Петрович"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["json"]["Должность"], "Разнорабочий"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
	Результат = КоннекторHTTP.PutJson("http://httpbin.org/put", Json); //@non-nls-1
	УтверждениеВерно(Результат["json"]["Сотрудник"], "Иванов Иван Петрович"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["json"]["Должность"], "Разнорабочий"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_Таймаут() Экспорт
	
	Попытка
		КоннекторHTTP.Get(
			"https://httpbin.org/delay/10", //@non-nls-1
			Неопределено,
			Новый Структура("Таймаут", 1)
		);
	Исключение
		ВерноеИсключение(
			ИнформацияОбОшибке(),
			СтрРазделить("Превышено время ожидания|Timeout exceeded", "|") //@non-nls-1
		);
	КонецПопытки;
	
КонецПроцедуры

Процедура Тест_ПолучитьGZip() Экспорт
	
	Результат = КоннекторHTTP.GetJson("http://httpbin.org/gzip"); //@non-nls-1
	УтверждениеВерно(Результат["gzipped"], Истина); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_ОтправитьGZip() Экспорт
	
	Json = Новый Структура;
	Json.Вставить("field", "value"); //@non-nls-1 //@non-nls-2
	Json.Вставить("field2", "value2"); //@non-nls-1 //@non-nls-2
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Encoding", "gzip"); //@non-nls-1 //@non-nls-2
	Результат = КоннекторHTTP.PostJson(
		"http://httpbin.org/anything", //@non-nls-1
		Json,
		Новый Структура("Заголовки", Заголовки)
	);
	
	ТелоЗапроса = ПолучитьДвоичныеДанныеИзBase64Строки(
		СтрРазделить(Результат["data"], ",")[1] //@non-nls-1
	);
	ИсходноеЗначение = КоннекторHTTP.JsonВОбъект(КоннекторHTTP.ПрочитатьGZip(ТелоЗапроса));
	
	УтверждениеВерно(Результат["headers"]["Content-Encoding"], "gzip"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(ИсходноеЗначение["field"], Json["field"]); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(ИсходноеЗначение["field2"], Json["field2"]); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_BasicAuth() Экспорт
	
	Результат = КоннекторHTTP.GetJson("https://user:pass@httpbin.org/basic-auth/user/pass"); //@non-nls-1
	УтверждениеВерно(Результат["authenticated"], Истина); //@non-nls-1
	УтверждениеВерно(Результат["user"], "user"); //@non-nls-1 //@non-nls-2

	Аутентификация = КоннекторHTTP.НоваяАутентификацияBasic();
	Аутентификация.Пользователь = "user"; //@non-nls-1
	Аутентификация.Пароль = "pass"; //@non-nls-1
	
	Результат = КоннекторHTTP.GetJson(
		"https://httpbin.org/basic-auth/user/pass", //@non-nls-1
		Неопределено,
		Новый Структура("Аутентификация", Аутентификация));
	УтверждениеВерно(Результат["authenticated"], Истина); //@non-nls-1
	УтверждениеВерно(Результат["user"], "user"); //@non-nls-1 //@non-nls-2

	Аутентификация = Новый Структура("Пользователь, Пароль, Тип");
	Аутентификация.Тип = "Basic"; //@non-nls-1
	Аутентификация.Пользователь = "user"; //@non-nls-1
	Аутентификация.Пароль = "pass"; //@non-nls-1
	
	Результат = КоннекторHTTP.GetJson(
		"https://httpbin.org/basic-auth/user/pass", //@non-nls-1
		Неопределено,
		Новый Структура("Аутентификация", Аутентификация));
	УтверждениеВерно(Результат["authenticated"], Истина); //@non-nls-1
	УтверждениеВерно(Результат["user"], "user"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_DigestAuth() Экспорт
	
	Аутентификация = КоннекторHTTP.НоваяАутентификацияDigest();
	Аутентификация.Пользователь = "user"; //@non-nls-1
	Аутентификация.Пароль = "pass"; //@non-nls-1
	
	Результат = КоннекторHTTP.GetJson(
		"https://httpbin.org/digest-auth/auth/user/pass", //@non-nls-1
		Неопределено,
		Новый Структура("Аутентификация", Аутентификация)
	);
	УтверждениеВерно(Результат["authenticated"], Истина); //@non-nls-1
	УтверждениеВерно(Результат["user"], "user"); //@non-nls-1 //@non-nls-2
	
	Аутентификация = КоннекторHTTP.НоваяАутентификацияDigest();
	Аутентификация.Пользователь = "guest"; //@non-nls-1
	Аутентификация.Пароль = "guest"; //@non-nls-1
	
	Результат = КоннекторHTTP.Get(
		"https://jigsaw.w3.org/HTTP/Digest/", //@non-nls-1
		Неопределено,
		Новый Структура("Аутентификация", Аутентификация));
	УтверждениеВерно(Результат.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_GetJson() Экспорт
	
	Результат = КоннекторHTTP.GetJson("https://httpbin.org/get"); //@non-nls-1
	УтверждениеВерно(Результат["url"], "https://httpbin.org/get"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_GetJsonСтруктура() Экспорт
	
	ПараметрыПреобразованияJSON = Новый Структура("ПрочитатьВСоответствие", Ложь);
	Результат = КоннекторHTTP.GetJson(
		"http://httpbin.org/json", //@non-nls-1
		Неопределено,
		Новый Структура("ПараметрыПреобразованияJSON", ПараметрыПреобразованияJSON)
	);
	УтверждениеВерно(Результат.slideshow.author, "Yours Truly"); //@non-nls-1
	УтверждениеВерно(Результат.slideshow.date, "date of publication"); //@non-nls-1
	УтверждениеВерно(Результат.slideshow.slides.Количество(), 2); //@non-nls-1
	УтверждениеВерно(Результат.slideshow.title, "Sample Slide Show"); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_PostJson() Экспорт
	
	ПараметрыПреобразованияJSON = Новый Структура;
	ПараметрыПреобразованияJSON.Вставить("ИменаСвойствСоЗначениямиДата", "Дата"); //@non-nls-2
	
	Json = Новый Структура;
	Json.Вставить("Дата", '20190121002400'); //@non-nls-1
	Json.Вставить("Число", 5); //@non-nls-1
	Json.Вставить("Булево", True); //@non-nls-1
	Json.Вставить("Строка", "Привет"); //@non-nls-1 //@non-nls-2
		
	Результат = КоннекторHTTP.PostJson(
		"https://httpbin.org/post", //@non-nls-1
		Json, 
		Новый Структура("ПараметрыПреобразованияJSON", ПараметрыПреобразованияJSON)
	);
	УтверждениеВерно(Результат["url"], "https://httpbin.org/post"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["Дата"], '20190121002400'); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["Число"], 5); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["Булево"], True); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["Строка"], "Привет"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_ПроверкаПреобразованияВариантаЗаписиДатыJSON() Экспорт

	УниверсальнаяКонтрольнаяДата = ТекущаяУниверсальнаяДата();
	ЛокальнаяКонтрольнаяДата = МестноеВремя(УниверсальнаяКонтрольнаяДата, ЧасовойПояс());
	Json = Новый Структура;
	Json.Вставить("Дата", ЛокальнаяКонтрольнаяДата); //@non-nls-1

	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("ВариантЗаписиДатыJSON", ПредопределенноеЗначение("ВариантЗаписиДатыJSON.УниверсальнаяДата"));

	Результат = КоннекторHTTP.PostJson(
		"https://httpbin.org/post", //@non-nls-1
		Json,
		Новый Структура("ПараметрыПреобразованияJSON", ПараметрыJSON)
	);

	УтверждениеВерно(
		Результат["json"]["Дата"], //@non-nls-1 //@non-nls-2
		Формат(УниверсальнаяКонтрольнаяДата, "ДФ=yyyy-MM-ddTHH:mm:ssZ")
	);

КонецПроцедуры

Процедура Тест_ПроверкаПреобразованияВJsonНеСериализуемыхЗначений() Экспорт

	Json = Новый Структура;
	Json.Вставить("УникальныйИдентификатор", Новый УникальныйИдентификатор("be4ee795-7f5e-4d1a-be43-a6d6902f5cfd")); //@non-nls-1
	Json.Вставить("ДвоичныеДанные", ПолучитьДвоичныеДанныеИзСтроки("test", "utf-8", Ложь)); //@non-nls-1 //@non-nls-2

	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("ИмяФункцииПреобразования", "ПреобразованиеJson");
	ПараметрыJSON.Вставить("МодульФункцииПреобразования", КоннекторHTTP);

	Результат = КоннекторHTTP.PostJson(
		"https://httpbin.org/post", //@non-nls-1
		Json,
		Новый Структура("ПараметрыПреобразованияJSON", ПараметрыJSON));

	УтверждениеВерно(Результат["json"]["УникальныйИдентификатор"], "be4ee795-7f5e-4d1a-be43-a6d6902f5cfd"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["json"]["ДвоичныеДанные"], "dGVzdA=="); //@non-nls-1 //@non-nls-2 //@non-nls-3

КонецПроцедуры

Процедура Тест_ПроверкаВосстановленияНеподдерживаемыхТиповЗначений() Экспорт

	УникальныйИдентификатор = Новый УникальныйИдентификатор("be4ee795-7f5e-4d1a-be43-a6d6902f5cfd"); //@non-nls-1
	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки("test", "utf-8", Ложь); //@non-nls-1 //@non-nls-2

	Json = Новый Структура;
	Json.Вставить("УникальныйИдентификатор", Строка(УникальныйИдентификатор)); //@non-nls-1
	Json.Вставить("ДвоичныеДанные", ПолучитьBase64СтрокуИзДвоичныхДанных(ДвоичныеДанные)); //@non-nls-1
	Json.Вставить("ДругиеДанные", 1); //@non-nls-1

	ПараметрыJSON = Новый Структура;
	ПараметрыJSON.Вставить("МодульФункцииВосстановления", КоннекторHTTP);
	ПараметрыJSON.Вставить("ИмяФункцииВосстановления", "ВосстановлениеJson");
	ТипыСвойств = Новый Соответствие;
	ТипыСвойств.Вставить("УникальныйИдентификатор", Тип("УникальныйИдентификатор")); //@non-nls-1
	ТипыСвойств.Вставить("ДвоичныеДанные", Тип("ДвоичныеДанные")); //@non-nls-1
	
	ПараметрыJSON.Вставить("ДополнительныеПараметрыФункцииВосстановления", ТипыСвойств);
	ПараметрыJSON.Вставить("ИменаСвойствДляОбработкиВосстановления", СтрРазделить("УникальныйИдентификатор,ДвоичныеДанные", ",")); //@non-nls-2

	Результат = КоннекторHTTP.PostJson(
		"https://httpbin.org/post", //@non-nls-1
		Json,
		Новый Структура("ПараметрыПреобразованияJSON", ПараметрыJSON));

	УтверждениеВерно(Результат["json"]["УникальныйИдентификатор"], УникальныйИдентификатор); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["ДвоичныеДанные"], ДвоичныеДанные); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["ДругиеДанные"], 1); //@non-nls-1 //@non-nls-2

КонецПроцедуры

Процедура Тест_PostИРедирект() Экспорт
	
	Ответ = КоннекторHTTP.Get("https://httpbin.org/redirect-to?url=https%3A%2F%2Fya.ru&status_code=301"); //@non-nls-1
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
	Ответ = КоннекторHTTP.Post("https://httpbin.org/redirect-to?url=https%3A%2F%2Fya.ru&status_code=301"); //@non-nls-1
	УтверждениеВерно(Ответ.КодСостояния, 403);
	
КонецПроцедуры

Процедура Тест_PutJson() Экспорт
	
	Результат = КоннекторHTTP.PutJson(
		"https://httpbin.org/put", //@non-nls-1
		Новый Структура("Название", "КоннекторHTTP") //@non-nls-1 //@non-nls-2
	);
	УтверждениеВерно(Результат["url"], "https://httpbin.org/put"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["Название"], "КоннекторHTTP"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_DeleteJson() Экспорт
	
	Результат = КоннекторHTTP.DeleteJson(
		"https://httpbin.org/delete", //@non-nls-1
		Новый Структура("Название", "КоннекторHTTP") //@non-nls-2
	);
	УтверждениеВерно(Результат["url"], "https://httpbin.org/delete"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["json"]["Название"], "КоннекторHTTP"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_GetУспешныйРедиректОтносительныйАдрес() Экспорт
	
	Ответ = КоннекторHTTP.Get("https://httpbin.org/relative-redirect/6"); //@non-nls-1
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Результат["url"], "https://httpbin.org/get"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_GetУспешныйРедиректАбсолютныйАдрес() Экспорт
	
	Ответ = КоннекторHTTP.Get("http://httpbin.org/absolute-redirect/6"); //@non-nls-1
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Результат["url"], "http://httpbin.org/get"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_GetЗацикленныйРедирект() Экспорт
	
	Попытка
		КоннекторHTTP.КакJson(КоннекторHTTP.Get("http://httpbin.org/redirect/31")); //@non-nls-1
	Исключение
		ВерноеИсключение(ИнформацияОбОшибке(), "СлишкомМногоПеренаправлений"); //@non-nls-1
	КонецПопытки;
	
КонецПроцедуры

Процедура Тест_GetОтключенныйРедирект() Экспорт
	
	Параметры = Новый Структура("РазрешитьПеренаправление", Ложь);
	Ответ = КоннекторHTTP.Get(
		"http://httpbin.org/redirect-to?url=http%3A%2F%2Fhttpbin.org%2Fget&status_code=307", //@non-nls-1
		Неопределено,
		Параметры);
	
	УтверждениеВерно(Ответ.КодСостояния, 307);
	
КонецПроцедуры

Процедура Тест_РедиректСУказаниемURL() Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("url", "https://httpbin.org:443/anything"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("status_code", "307"); //@non-nls-1 //@non-nls-2

	Ответ = КоннекторHTTP.Get("http://httpbin.org:80/redirect-to", ПараметрыЗапроса); //@non-nls-1
	КоннекторHTTP.КакJson(Ответ);

	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Ответ.URL, "https://httpbin.org:443/anything"); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_Ошибка404() Экспорт
	
	Ответ = КоннекторHTTP.Get("http://httpbin.org/status/404"); //@non-nls-1
	
	УтверждениеВерно(Ответ.КодСостояния, 404);
	
КонецПроцедуры

Процедура Тест_РаботаССессиями() Экспорт
	
	Сессия = КоннекторHTTP.СоздатьСессию();
	
	КоннекторHTTP.Get("https://httpbin.org/cookies/set/key/value", Неопределено, Неопределено, Сессия); //@non-nls-1
	Результат = КоннекторHTTP.GetJson("https://httpbin.org/cookies", Неопределено, Неопределено, Сессия); //@non-nls-1
	
	УтверждениеВерно(Результат["cookies"]["key"], "value"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_ПолучениеСпискаРелизовВСайта1С() Экспорт
	
	Сессия = КоннекторHTTP.СоздатьСессию();
	Ответ = КоннекторHTTP.Get("https://releases.1c.ru/total", Неопределено, Неопределено, Сессия); //@non-nls-1
	
	Данные = Новый Структура;
	Данные.Вставить("execution", ИзвлечьExecution(Ответ)); //@non-nls-1
	Данные.Вставить("username", Логин); //@non-nls-1
	Данные.Вставить("password", Пароль); //@non-nls-1
	Данные.Вставить("_eventId", "submit"); //@non-nls-1 //@non-nls-2
	Данные.Вставить("geolocation", ""); //@non-nls-1
	Данные.Вставить("submit", "Войти"); //@non-nls-1 //@non-nls-2
	Данные.Вставить("rememberMe", "on"); //@non-nls-1 //@non-nls-2
	
	Ответ = КоннекторHTTP.Post(Ответ.URL, Данные, Неопределено, Сессия);
	
	УтверждениеВерно(Ответ.URL, "https://releases.1c.ru/total"); //@non-nls-1
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_Options() Экспорт
	
	Ответ = КоннекторHTTP.Options("http://httpbin.org/anything"); //@non-nls-1
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_Head() Экспорт
	
	Ответ = КоннекторHTTP.Head("http://httpbin.org/anything"); //@non-nls-1
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_Delete() Экспорт
	
	Ответ = КоннекторHTTP.Delete("http://httpbin.org/delete"); //@non-nls-1
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_Patch() Экспорт
	
	Ответ = КоннекторHTTP.Patch("http://httpbin.org/patch"); //@non-nls-1
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_ПроизвольныйМетод() Экспорт
	
	Ответ = КоннекторHTTP.ВызватьМетод("PATCH", "http://httpbin.org/patch"); //@non-nls-1 //@non-nls-2
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_УстановкаCookies() Экспорт
	
	Результат = КоннекторHTTP.GetJson(
		"http://httpbin.org/cookies/set?PHPSESSID=72a68cc1e55&cookie1=1&cookie2=2&other=test"); //@non-nls
	
	УтверждениеВерно(Результат["cookies"]["PHPSESSID"], "72a68cc1e55"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["cookies"]["cookie1"], "1"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["cookies"]["cookie2"], "2"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["cookies"]["other"], "test"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_ОтправитьCookies() Экспорт
	
	Cookies = Новый Массив;
	Cookies.Добавить(Новый Структура("Наименование,Значение", "k1", Строка(Новый УникальныйИдентификатор))); //@non-nls-2
	Cookies.Добавить(Новый Структура("Наименование,Значение", "k2", Строка(Новый УникальныйИдентификатор))); //@non-nls-2
	
	Ответ = КоннекторHTTP.Get(
		"http://httpbin.org/cookies", //@non-nls-1
		,
		Новый Структура("Cookies", Cookies)
	);
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Результат["cookies"]["k1"], Cookies[0].Значение); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["cookies"]["k2"], Cookies[1].Значение); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_POST_MultipartFormData_ТолькоФайл() Экспорт
	
	Файлы = Новый Структура;
	Файлы.Вставить("Имя", "f1"); //@non-nls-2
	Файлы.Вставить("ИмяФайла", "file1.txt"); //@non-nls-2
	Файлы.Вставить("Данные", Base64Значение("0J/RgNC40LLQtdGCLCDQnNC40YA=")); //@non-nls-2
	Файлы.Вставить("Тип", "text/plain"); //@non-nls-2
	
	Результат = КоннекторHTTP.PostJson(
		"https://httpbin.org/post", //@non-nls-1
		Неопределено,
		Новый Структура("Файлы", Файлы)
	);
	
	УтверждениеВерно(Результат["files"]["f1"], "Привет, Мир"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_POST_MultipartFormData_ФайлыИПоляФормы() Экспорт
	
	Файлы = Новый Массив;
	Файлы.Добавить(Новый Структура("Имя,Данные,ИмяФайла", "f1", Base64Значение("ZmlsZTE="), "file1.txt")); //@non-nls-2 //@non-nls-3 //@non-nls-4
	Файлы.Добавить(Новый Структура("Имя,Данные,ИмяФайла", "f2", Base64Значение("ZmlsZTI="), "file2.txt")); //@non-nls-2 //@non-nls-3 //@non-nls-4
	
	Данные = Новый Структура("field1,field2", "value1", "Значение2"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
	Результат = КоннекторHTTP.PostJson(
		"https://httpbin.org/post", //@non-nls-1
		Неопределено,
		Новый Структура("Файлы,Данные", Файлы, Данные)
	);
	
	УтверждениеВерно(Результат["files"]["f1"], "file1"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["files"]["f2"], "file2"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["form"]["field1"], "value1"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["form"]["field2"], "Значение2"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_POST_MultipartFormData_ФайлыИПоляФормы_КонструкторПараметров() Экспорт
	
	ДополнительныеПараметры = КоннекторHTTP.НовыеПараметры();
	ДополнительныеПараметры.Файлы.Добавить(
		КоннекторHTTP.НовыйОтправляемыйФайл("f1", "file1.txt", Base64Значение("ZmlsZTE=")) //@non-nls-1 //@non-nls-2 //@non-nls-3
	);
	ДополнительныеПараметры.Файлы.Добавить(
		КоннекторHTTP.НовыйОтправляемыйФайл("f2", "file2.txt", Base64Значение("ZmlsZTI=")) //@non-nls-1 //@non-nls-2 //@non-nls-3
	);
	ДополнительныеПараметры.Файлы.Добавить(
		КоннекторHTTP.НовыйОтправляемыйФайл(
			"f3", //@non-nls-1
			"file3.txt", //@non-nls-1
			Base64Значение("Z0J/RgNC40LLQtdGCLCDQnNC40YA="), //@non-nls-1
			"text/plain" //@non-nls-1
		)
	);
	Файл = КоннекторHTTP.НовыйОтправляемыйФайл("f4", "file4.txt"); //@non-nls-1 //@non-nls-2
	Файл.Заголовки.Вставить("X-ID", Строка(Новый УникальныйИдентификатор));
	ДополнительныеПараметры.Файлы.Добавить(Файл);
	
	ДополнительныеПараметры.Данные = Новый Структура;
	ДополнительныеПараметры.Данные.Вставить("field1", "value1"); //@non-nls-1 //@non-nls-2
	ДополнительныеПараметры.Данные.Вставить("field2", "Значение2"); //@non-nls-1 //@non-nls-2
	
	Результат = КоннекторHTTP.PostJson(
		"https://httpbin.org/post", //@non-nls-1
		Неопределено,
		ДополнительныеПараметры
	);
	
	УтверждениеВерно(Результат["files"]["f1"], "file1"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["files"]["f2"], "file2"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["files"]["f3"], "data:text/plain;base64,Z0J/RgNC40LLQtdGCLCDQnNC40YA"); // Должно быть "Привет, Мир" //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["files"]["f4"], ""); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["form"]["field1"], "value1"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	УтверждениеВерно(Результат["form"]["field2"], "Значение2"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
КонецПроцедуры

Процедура Тест_ПараметрыЗапросаТолькоКлюч() Экспорт

	Результат = КоннекторHTTP.GetJson("https://httpbin.org/get?key"); //@non-nls-1
	УтверждениеВерно(Результат["args"]["key"], ""); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_ОтправкаXml() Экспорт
	
	XML = 
	"<?xml version=""1.0"" encoding=""utf-8""?>
	|<soap:Envelope xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" xmlns:xsd=""http://www.w3.org/2001/XMLSchema"" xmlns:soap=""http://schemas.xmlsoap.org/soap/envelope/"">
	|  <soap:Body>
	|    <GetCursOnDate xmlns=""http://web.cbr.ru/"">
	|      <On_date>2019-07-05</On_date>
	|    </GetCursOnDate>
	|  </soap:Body>
	|</soap:Envelope>"; //@non-nls-1
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "text/xml; charset=utf-8"); //@non-nls-1 //@non-nls-2
	Заголовки.Вставить("SOAPAction", "http://web.cbr.ru/GetCursOnDate"); //@non-nls-1 //@non-nls-2
	Ответ = КоннекторHTTP.Post(
		"https://www.cbr.ru/DailyInfoWebServ/DailyInfo.asmx", //@non-nls-1
		XML, 
		Новый Структура("Заголовки", Заголовки));
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Ответ.Заголовки.Получить("Content-Type"), "text/xml; charset=utf-8"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_ПрочитатьОтветКакXDTO() Экспорт
	
	ТекстОтветXML = Тест_ПрочитатьОтветКакXDTO_ТекстОтветаXML();
	
	ПолноеИмяФайла = ПолучитьИмяВременногоФайла("xml"); //@non-nls-1
	
	ФайлТекст = Новый ЗаписьТекста(ПолноеИмяФайла, КодировкаТекста.UTF8); 
	ФайлТекст.ЗаписатьСтроку(ТекстОтветXML);
	ФайлТекст.Закрыть(); 
	
	ДвоичныеДанныеОтвет = Новый ДвоичныеДанные(ПолноеИмяФайла);
	УдалитьФайлы(ПолноеИмяФайла);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "text/xml; charset=utf-8"); //@non-nls-1 //@non-nls-2
	
	ПодготовленныйОтвет = КоннекторHTTP.НовыйОтвет();
	ПодготовленныйОтвет.Тело = ДвоичныеДанныеОтвет;
	ПодготовленныйОтвет.Заголовки = Заголовки;
	
	ОтветXDTO = КоннекторHTTP.КакXDTO(ПодготовленныйОтвет);
	
	ОтветXDTO_Attachment = ОтветXDTO.Attachments.Attachment;
	
	УтверждениеВерно(ТипЗнч(ОтветXDTO), Тип("ОбъектXDTO"));
	УтверждениеВерно(ОтветXDTO.Id, "1642606"); //@non-nls-1
	УтверждениеВерно(ТипЗнч(ОтветXDTO_Attachment), Тип("СписокXDTO"));
	УтверждениеВерно(ОтветXDTO_Attachment.Количество(), 2);
	
КонецПроцедуры 

Функция Тест_ПрочитатьОтветКакXDTO_ТекстОтветаXML() 
	
	ТекстОтветXML = "
	|<Document>
	|<Id>1642606</Id>
	|<TypeId>65</TypeId>
	|<TypeName>Документ</TypeName>
	|<DocumentTypeCode>DOCUMENT</DocumentTypeCode>
	|<Description>Документ тест 00001 от 21.05.2021</Description>
	|<BarcodeType>None</BarcodeType>
	|<SenderId>12776</SenderId>
	|<ReceiverId>48519</ReceiverId>
	|<ParticipantId>12776</ParticipantId>
	|<Status>Received</Status>
	|<Date>2021-05-21T00:00:00</Date>
	|<RequestSign>true</RequestSign>
	|<Number>ТЕСТ 00001</Number>
	|<IsMarked>false</IsMarked>
	|<Created>2021-05-21T10:32:02</Created>
	|<SendDate>2021-05-21T10:32:43</SendDate>
	|<ReceiveDate>2021-05-21T10:32:43</ReceiveDate>
	|<IsDocflowCompleted>false</IsDocflowCompleted>
	|<Comment>ТЕСТ Документ </Comment>
	|<Type>Document</Type>
	|<FormType>Document</FormType>
	|<SellerCode>ИдентификаторЭДООтправитель</SellerCode>
	|<BuyerCode>ИдентификаторЭДОПолучатель</BuyerCode>
	|<Filename>ФайлОсновной.xlsx</Filename>
	|<ContractDescription />
	|<IsRoaming>false</IsRoaming>
	|<IsPrintable>false</IsPrintable>
	|<Relations>
	|    <DocumentRelation>
	|        <Id>1642606</Id>
	|        <Description>Документ №ТЕСТ 00002 от 21.05.2021</Description>
	|    </DocumentRelation>
	|</Relations>
	|<StatusName>Получен</StatusName>
	|<SenderName>ООО Отправитель</SenderName>
	|<ReceiverName>ООО Получатель</ReceiverName>
	|<ExistRouteSigner>false</ExistRouteSigner>
	|<DocumentRelationType>Productive</DocumentRelationType>
	|<Attachments>
	|    <Attachment>
	|        <Id>44624</Id>
	|        <Uuid>8711ca1b76794467ba74529d2bb01e3e</Uuid>
	|        <DocumentId>0</DocumentId>
	|        <Filename>ТестФайл.xlsx</Filename>
	|        <Content />
	|    </Attachment>
	|    <Attachment>
	|        <Id>44625</Id>
	|        <Uuid>f6d8e6cdcbf846f4b97d5ab8e18d1b79</Uuid>
	|        <DocumentId>0</DocumentId>
	|        <Filename>ТестФайл2.pdf</Filename>
	|        <Content />
	|    </Attachment>
	|</Attachments>
	|<StatusChanged>2021-05-21T10:32:44</StatusChanged>
	|</Document>	
	|"; //@non-nls-1
	
	Возврат ТекстОтветXML;
	
КонецФункции 

Процедура Тест_СложныеПараметрыЗапроса() Экспорт

	Результат = КоннекторHTTP.GetJson(
		"https://httpbin.org/anything?jql=worklogDate >= 2017-04-01 AND worklogDate <= 2017-05-01&j&i=2"); //@non-nls-1
	УтверждениеВерно(
		Результат["args"]["jql"], //@non-nls-1 //@non-nls-2
		"worklogDate >= 2017-04-01 AND worklogDate <= 2017-05-01" //@non-nls-1
	);
	УтверждениеВерно(Результат["args"]["j"], ""); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(Результат["args"]["i"], "2"); //@non-nls-1 //@non-nls-2
	
КонецПроцедуры

Процедура Тест_PostПустойJson() Экспорт
	
	Json = Новый Структура;
	Результат = КоннекторHTTP.PostJson("https://httpbin.org/post", Json); //@non-nls-1
	УтверждениеВерно(Результат["url"], "https://httpbin.org/post"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(
		ТипЗнч(Результат["json"]), //@non-nls-1
		Тип("Соответствие")
	);
	
КонецПроцедуры

Процедура Тест_АутентификацияAWS4_HMAC_SHA256() Экспорт
	
	Аутентификация = КоннекторHTTP.НоваяАутентификацияAWS4();
	Аутентификация.ИдентификаторКлючаДоступа = ИдентификаторКлючаДоступа;
	Аутентификация.СекретныйКлюч = СекретныйКлюч;
	Аутентификация.Регион = Регион;
	Аутентификация.Сервис = "sqs"; //@non-nls-1
	
	URL = СтрШаблон("https://sqs.%1.amazonaws.com:443%2", Регион, Очередь); //@non-nls-1
	
	ПараметрыЗапроса = Новый Соответствие;
	ПараметрыЗапроса.Вставить("Action", "SendMessage"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("MessageBody", "This is a test message"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("MessageAttribute.1.Name", "my_attribute_name_1"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("MessageAttribute.1.Value.StringValue", "my_attribute_value_1"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("MessageAttribute.1.Value.DataType", "String"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("MessageAttribute.2.Name", "my_attribute_name_2"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("MessageAttribute.2.Value.StringValue", "my_attribute_value_2"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("MessageAttribute.2.Value.DataType", "String"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("Expires", "2020-05-05T22%3A52%3A43PST"); //@non-nls-1 //@non-nls-2
	ПараметрыЗапроса.Вставить("Version", "2012-11-05"); //@non-nls-1 //@non-nls-2
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Аутентификация", Аутентификация);
	Ответ = КоннекторHTTP.Get(URL, ПараметрыЗапроса, ДополнительныеПараметры);
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_УпаковатьGZip() Экспорт
	
	ИсходныеДанные = ПолучитьДвоичныеДанныеИзСтроки("Привет, Мир!", КодировкаТекста.UTF8, Ложь); //@non-nls-1
	СжатыеДанные = ПолучитьДвоичныеДанныеИзBase64Строки("H4sIAAAAAAAA/wEVAOr/0J/RgNC40LLQtdGCLCDQnNC40YAhilS6PhUAAAA="); //@non-nls-1
	
	УтверждениеВерно(КоннекторHTTP.ЗаписатьGZip(ИсходныеДанные), СжатыеДанные);

КонецПроцедуры

Процедура Тест_РаспаковатьGZip() Экспорт
	
	СжатыеДанные = ПолучитьДвоичныеДанныеИзBase64Строки("H4sIAAAAAAAA/wEVAOr/0J/RgNC40LLQtdGCLCDQnNC40YAhilS6PhUAAAA="); //@non-nls-1
	Данные = КоннекторHTTP.ПрочитатьGZip(СжатыеДанные);
	
	УтверждениеВерно(ПолучитьСтрокуИзДвоичныхДанных(Данные, КодировкаТекста.UTF8), "Привет, Мир!"); //@non-nls-1
	
КонецПроцедуры

Процедура Тест_АутентификацияAWS4_HMAC_SHA256_ПустоеТелоПортНеУказан() Экспорт
	
	URL = "https://sqs.eu-north-1.amazonaws.com/123456789101/test"; //@non-nls-1
	
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "AWS4-HMAC-SHA256"); //@non-nls-2
	Аутентификация.Вставить("ИдентификаторКлючаДоступа", "ACCESS_KEY"); //@non-nls-2
	Аутентификация.Вставить("СекретныйКлюч", "SECRET_KEY"); //@non-nls-2
	Аутентификация.Вставить("Регион", "eu-north-1"); //@non-nls-2
	Аутентификация.Вставить("Сервис", "sqs"); //@non-nls-2

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("x-amz-date", "20200426T122000Z"); //@non-nls-1 //@non-nls-2
	
	Сессия = КоннекторHTTP.СоздатьСессию();
	Сессия.Заголовки = Заголовки;
	Сессия.Аутентификация = Аутентификация;
	
	ПодготовленныйЗапрос = КоннекторHTTP.ПодготовитьЗапрос(Сессия, "GET", URL, Новый Структура);  //@non-nls-1
	
	УтверждениеВерно(ПодготовленныйЗапрос.Заголовки["x-amz-date"], "20200426T122000Z");  //@non-nls-1 //@non-nls-2
	УтверждениеВерно(
		ПодготовленныйЗапрос.Заголовки["x-amz-content-sha256"], //@non-nls-1
		"e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855" //@non-nls-1
	);
	УтверждениеВерно(
		ПодготовленныйЗапрос.Заголовки["Authorization"], //@non-nls-1
		"AWS4-HMAC-SHA256 Credential=ACCESS_KEY/20200426/eu-north-1/sqs/aws4_request, SignedHeaders=host;x-amz-content-sha256;x-amz-date, Signature=ad98a02ef5df6b5337128ccdac1e5a279846c0d1c4b409e713a75dae0c9e4cb3" //@non-nls-1
	);
	
КонецПроцедуры

Процедура Тест_АутентификацияAWS4_HMAC_SHA256_ПустоеТелоУказанСтандартныйПорт() Экспорт
	
	URL = "https://sqs.eu-north-1.amazonaws.com:443/123456789101/test"; //@non-nls-1
	
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "AWS4-HMAC-SHA256"); //@non-nls-2
	Аутентификация.Вставить("ИдентификаторКлючаДоступа", "ACCESS_KEY"); //@non-nls-2
	Аутентификация.Вставить("СекретныйКлюч", "SECRET_KEY"); //@non-nls-2
	Аутентификация.Вставить("Регион", "eu-north-1"); //@non-nls-2
	Аутентификация.Вставить("Сервис", "sqs"); //@non-nls-2

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("x-amz-date", "20200426T122000Z"); //@non-nls-1 //@non-nls-2
	
	Сессия = КоннекторHTTP.СоздатьСессию();
	Сессия.Заголовки = Заголовки;
	Сессия.Аутентификация = Аутентификация;
	
	ПодготовленныйЗапрос = КоннекторHTTP.ПодготовитьЗапрос(Сессия, "GET", URL, Новый Структура); //@non-nls-1
	
	УтверждениеВерно(ПодготовленныйЗапрос.Заголовки["x-amz-date"], "20200426T122000Z"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(ПодготовленныйЗапрос.Заголовки["x-amz-content-sha256"], "e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(
		ПодготовленныйЗапрос.Заголовки["Authorization"], //@non-nls-1
		"AWS4-HMAC-SHA256 Credential=ACCESS_KEY/20200426/eu-north-1/sqs/aws4_request, SignedHeaders=host;x-amz-content-sha256;x-amz-date, Signature=ad98a02ef5df6b5337128ccdac1e5a279846c0d1c4b409e713a75dae0c9e4cb3" //@non-nls-1
	);
	
КонецПроцедуры

Процедура Тест_АутентификацияAWS4_HMAC_SHA256_ПустоеТелоУказанНеСтандартныйПорт() Экспорт
	
	URL = "https://sqs.eu-north-1.amazonaws.com:444/123456789101/test"; //@non-nls-1
	
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "AWS4-HMAC-SHA256"); //@non-nls-2
	Аутентификация.Вставить("ИдентификаторКлючаДоступа", "ACCESS_KEY"); //@non-nls-2
	Аутентификация.Вставить("СекретныйКлюч", "SECRET_KEY"); //@non-nls-2
	Аутентификация.Вставить("Регион", "eu-north-1"); //@non-nls-2
	Аутентификация.Вставить("Сервис", "sqs"); //@non-nls-2

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("x-amz-date", "20200426T122000Z"); //@non-nls-1 //@non-nls-2
	
	Сессия = КоннекторHTTP.СоздатьСессию();
	Сессия.Заголовки = Заголовки;
	Сессия.Аутентификация = Аутентификация;
	
	ПодготовленныйЗапрос = КоннекторHTTP.ПодготовитьЗапрос(Сессия, "GET", URL, Новый Структура); //@non-nls-1
	
	УтверждениеВерно(ПодготовленныйЗапрос.Заголовки["x-amz-date"], "20200426T122000Z"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(ПодготовленныйЗапрос.Заголовки["x-amz-content-sha256"], "e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855"); //@non-nls-1 //@non-nls-2
	УтверждениеВерно(
		ПодготовленныйЗапрос.Заголовки["Authorization"], //@non-nls-1
		"AWS4-HMAC-SHA256 Credential=ACCESS_KEY/20200426/eu-north-1/sqs/aws4_request, SignedHeaders=host;x-amz-content-sha256;x-amz-date, Signature=96df4fab493e08b8a72965acf7a6d2fd651a883a2766a2b0e4c0cd113dea7d8c" //@non-nls-1
	);
	
КонецПроцедуры

Процедура Тест_ПовторСУчетомЗаголовкаRetryAfterDuration() Экспорт
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-ID", Строка(Новый УникальныйИдентификатор)); //@non-nls-1
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МаксимальноеКоличествоПовторов", 5);
	ДополнительныеПараметры.Вставить("КоэффициентЭкспоненциальнойЗадержки", 2);
	
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);
		
	URL = "http://127.0.0.1:5000/retry_after_duration"; //@non-nls-1
	Ответ = КоннекторHTTP.Get(URL, Неопределено, ДополнительныеПараметры);
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_ПовторСУчетомЗаголовкаRetryAfterDate() Экспорт
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-ID", Строка(Новый УникальныйИдентификатор)); //@non-nls-1
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МаксимальноеКоличествоПовторов", 5);
	ДополнительныеПараметры.Вставить("КоэффициентЭкспоненциальнойЗадержки", 2);
	
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);
		
	URL = "http://127.0.0.1:5000/retry_after_date"; //@non-nls-1
	Ответ = КоннекторHTTP.Get(URL, Неопределено, ДополнительныеПараметры);
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_ПовторПослеОшибки502() Экспорт

	ПовторятьДляКодовСостояний = Новый Массив;
	ПовторятьДляКодовСостояний.Добавить(КоннекторHTTP.КодыСостоянияHTTP().ОшибочныйШлюз_502);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-ID", Строка(Новый УникальныйИдентификатор)); //@non-nls-1
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МаксимальноеКоличествоПовторов", 5);
	ДополнительныеПараметры.Вставить("ПовторятьДляКодовСостояний", ПовторятьДляКодовСостояний);
	
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);
		
	URL = "http://127.0.0.1:5000/retry_502"; //@non-nls-1
	Ответ = КоннекторHTTP.Get(URL, Неопределено, ДополнительныеПараметры);
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_ПовторПослеОшибкиПодключения() Экспорт

	ПовторятьДляКодовСостояний = Новый Массив;
	ПовторятьДляКодовСостояний.Добавить(КоннекторHTTP.КодыСостоянияHTTP().ОшибочныйШлюз_502);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-ID", Строка(Новый УникальныйИдентификатор)); //@non-nls-1
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Таймаут", 1);
	ДополнительныеПараметры.Вставить("МаксимальноеКоличествоПовторов", 2);
	ДополнительныеПараметры.Вставить("ПовторятьДляКодовСостояний", ПовторятьДляКодовСостояний);
	
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);
	
	Начало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	URL = "http://127.0.0.1:5001/non_existent_resource"; //@non-nls-1
	Попытка
		КоннекторHTTP.Get(URL, Неопределено, ДополнительныеПараметры);
	Исключение
		Длительность = ТекущаяУниверсальнаяДатаВМиллисекундах() - Начало;
	КонецПопытки;
	УтверждениеВерно(Длительность >= 3000 И Длительность < 7000, Истина);
	
КонецПроцедуры

Процедура Тест_Ping() Экспорт

	URL = "http://127.0.0.1:5000/ping"; //@non-nls-1
	Ответ = КоннекторHTTP.Get(URL);
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_ПредставлениеКодаСостоянияHTTP() Экспорт

	УтверждениеВерно(КоннекторHTTP.ПредставлениеКодаСостоянияHTTP(400), "400: Bad Request"); //@non-nls-1

КонецПроцедуры

Процедура Тест_ПредставлениеНеизвестногоКодаСостоянияHTTP() Экспорт

	Попытка
		КоннекторHTTP.ПредставлениеКодаСостоянияHTTP(488);
	Исключение
		ВерноеИсключение(ИнформацияОбОшибке(), "Неизвестный код состояния HTTP: 488"); //@non-nls-1
	КонецПопытки;
	
КонецПроцедуры

Процедура Тест_КорректноеИсключениеВМетодеКакJson() Экспорт

	Попытка
		КоннекторHTTP.GetJson("https://en.wikipedia.org/wiki/1C_Company#Products");//@non-nls-1
	Исключение
		ВерноеИсключение(ИнформацияОбОшибке(), "1C Company"); //@non-nls-1
	КонецПопытки;
	
КонецПроцедуры

Процедура Тест_ОтправкаЗапросаПортитПереданныеНастройки_GitHub_Issue_33() Экспорт
	
	Настройки = Новый Структура; 
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type","application/json"); //@non-nls-1 //@non-nls-2
	Настройки.Вставить("Заголовки", Заголовки);
	
	ПараметрыЗапроса = Новый Структура("username, password", "anonymous", "hrgesf7HDR67Bd"); //@non-nls-1 //@non-nls-2 //@non-nls-3
	
	НастройкиКопияДоОтправки = ЗначениеИзСтрокиВнутр(ЗначениеВСтрокуВнутр(Настройки));
	
	КоннекторHTTP.Post("https://jsonplaceholder.typicode.com/posts", ПараметрыЗапроса, Настройки); //@non-nls-1
	
	УтверждениеВерно(КоннекторHTTP.ОбъектВJson(Настройки), КоннекторHTTP.ОбъектВJson(НастройкиКопияДоОтправки));
	
КонецПроцедуры

#КонецОбласти

Функция ИзвлечьExecution(Ответ)
	
	HTML = КоннекторHTTP.КакТекст(Ответ);
	
	Начало = СтрНайти(HTML, "name=""execution"""); //@non-nls-1
	Конец = СтрНайти(HTML, ">", НаправлениеПоиска.СНачала, Начало);
	КороткаяСтрока = Сред(HTML, Начало, Конец - Начало);
	
	Начало = СтрНайти(КороткаяСтрока, "value=""") + СтрДлина("value="""); //@non-nls-1 //@non-nls-2
	Конец = СтрНайти(КороткаяСтрока, """", НаправлениеПоиска.СНачала, Начало); 
	
	Возврат Сред(КороткаяСтрока, Начало, Конец - Начало);
	
КонецФункции

Функция ПосчитатьMD5(Данные)
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.MD5);
	Хеширование.Добавить(Данные);
	
	Возврат НРег(ПолучитьHexСтрокуИзДвоичныхДанных(Хеширование.ХешСумма));
	
КонецФункции

Процедура УтверждениеВерно(ЛевоеЗначение, ПравоеЗначение, Пояснение = "")
	
	Если ЛевоеЗначение <> ПравоеЗначение Тогда
		ВызватьИсключение(СтрШаблон(НСтр("ru = '<%1> не равно <%2>: %3'"), ЛевоеЗначение, ПравоеЗначение, Пояснение));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВерноеИсключение(ИнформацияОбОшибке, ОжидаемоеИсключение)
	
	ТекстИсключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	Если ТипЗнч(ОжидаемоеИсключение) = Тип("Массив") Тогда
		ТекстНайден = Ложь;
		Для Каждого Элемент Из ОжидаемоеИсключение Цикл
			Если СтрНайти(ТекстИсключения, Элемент) Тогда
				ТекстНайден = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ТекстНайден Тогда
			ВызватьИсключение(ТекстИсключения);
		КонецЕсли;
	Иначе
		Если Не СтрНайти(ТекстИсключения, ОжидаемоеИсключение) Тогда
			ВызватьИсключение(ТекстИсключения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
